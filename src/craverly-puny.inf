!% -~S
!% $OMIT_UNUSED_ROUTINES=1
!% $MAX_ABBREVS=96
!% $MAX_DYNAMIC_STRINGS=0

Abbreviate " the " " you" "^^~" "Pauline" "You " " to " ", " " that" "Leopold " "Craverly" "ing " ". " "n't " "'s " " of " ".~^^" "Gina" " is " " Langridge" ",~" "her" " say" "his " "The" "?~" " and " "thing" " with " "jewels" " Wendell" " test results" " in" "'re " " again" "I " "meaningfully" "Sidney" "tion" "your " "menacingly" " for " ".~" " from " " have " " the" "~What" "ight" "ing" " are" "ed " "^^" "s." "er " "!~" " a " "Of course" "But " " expression" " can " "~I" " con" " no" " Doctor Langridg" "all" " as" "ould" "yourself" "ent" " she" " do" " out" "know" " it" "some" "e." " already" "the papers" " mysterious " " about " " what " " think" " prison" "you announce" " Janine" "I'm " " on" " re" " around" " be" "You" "ere we buried" "ough" "and" " hospital" "That" " look" ;

! Volume - Boring

Constant Story      "Craverly Heights";
Constant Headline   "^An Interactive Fiction by Ryan Veeder, ported to PunyInform by Johan Berntsson^";

Release 1;
Serial "210707";

! This IFID is unique for this port
Array UUID_ARRAY string "UUID://D2780FBF-CC11-4D25-98BB-827E8FCE4C7F//";
#Ifdef UUID_ARRAY;#Endif;

Constant STATUSLINE_TIME; Statusline time;
!Constant STATUSLINE_SCORE; Statusline score;

Constant NO_SCORE = 0;

#iftrue (#version_number > 3);
! undo and customizable statuslines are not available in v3
Constant OPTIONAL_SL_NO_SCORE;
Constant OPTIONAL_SL_NO_MOVES;
Constant OPTIONAL_PROVIDE_UNDO;
#endif;

Constant CUSTOM_ABBREVIATIONS;
Constant OPTIONAL_NO_DARKNESS;
#ifdef DEBUG;
! we need to run tests in debug mode since we need the transcript verb
Constant OPTIONAL_EXTENDED_METAVERBS;
#endif;

Constant INITIAL_LOCATION_VALUE = Hospital;

Include "globals.h";

Attribute dead;
Attribute onstage;
Attribute backstage;
Global prog = 0;
Global finalmessage;
Global suspect = Pauline;
Global sidshotleo = false;

[ DeathMessage;
    print (string) finalmessage;
];

Constant MSG_WAKE_SUCCESS 1000;
Constant MSG_WAIT_SUCCESS 1001;
Constant MSG_TOUCH_SUCCESS 1002;
Constant MSG_SING_DEFAULT 1003;
Constant MSG_SLEEP_DEFAULT 1004;
Constant MSG_THINK_DEFAULT 1005;
Constant MSG_STRONG_DEFAULT 1006;
Constant MSG_MILD_DEFAULT 1007;
Constant MSG_TAKE_SCENERY 1008;

[ LibraryMessages p_msg p_arg1 p_arg2;
    switch(p_msg) {
    MSG_TAKE_SCENERY:
        if(location has onstage) "Moving that is someone else's job.";
        "That belongs where it is.";
    MSG_STRONG_DEFAULT, MSG_MILD_DEFAULT:
        if(location has backstage) 
            "If that's what it takes to get you to calm down.";
        "You better control yourself: This is daytime TV.";
    MSG_THINK_DEFAULT:
        if(location has backstage) 
            "You pause to consider what choices have led to this situation.";
        if(Sunglasses has worn) 
            "You pause to think. But of course you, Sidney Langridge,
            can think only of yourself.";
        "~I need to think,~ you say, pressing your fingers against your
        temples. ~The answer is staring me right in the face, I just know
        it.~ You squint deeply, the weight of all Craverly Heights's
        problems upon your shoulders.";
    MSG_SLEEP_DEFAULT:
        if(location has backstage) "There's a lot left to do today.";
        "Not on the job.";
    MSG_SING_DEFAULT:
        if(location has backstage) "Not one of your talents.";
        "They don't pay you enough.";
    MSG_WAKE_SUCCESS:
        if(location has backstage) "This is too stupid to be a dream.";
        "~Maybe this is all a dream,~ you say, gazing into the distance,
        but no answer comes.";
    MSG_WAIT_SUCCESS:
        if(location has onstage) {
            print "You glance around ";
            if(Sunglasses has worn)
                print "menacingly";
            else
                print "meaningfully";
            ".";
        }
        "Time passes.";
    MSG_TOUCH_SUCCESS:
        if(location has backstage) {
            if(noun == player) "You feel fine.";
            if(noun has animate) 
                "You touch ", (the) noun, ", who tries to shoo you away.";
            print_ret (The) noun, " feels normal.";
        } else {
            if(noun == player) "You press a finger against your skin.";
            if(noun has animate) 
                "You put your hand against ", (the) noun, "'s face. ",
                (The) noun, " looks meaningfully back at you.";
            "You pause to consider the texture of ", (the) noun, ".";
        }
    }
    p_arg1 = 0;
    p_arg2 = 0;
];

Include "puny.h";

Global has_unicode;

! This writes emdash (unicode 8212) if possible, otherwise " - "
[ u8212;
#iftrue (#version_number >= 5);
    if(has_unicode) {
        @print_unicode 8212;
        return;
    }
#endif;
    print " - ";
];

[ fixed_letter_spacing; font off; ];
[ variable_letter_spacing; font on; ];
#iftrue (#version_number >= 4);
[ italic_type; style underline; ]; ! or bold?
[ roman_type; style roman; ];
#ifnot;
! no fancy text variations in v3
[ italic_type; ];
[ roman_type; ];
#endif;


! This writes 3 * B039 (unicode 10020) if possible, otherwise "???"
[ u10020;
#iftrue (#version_number >= 5);
    if(has_unicode) {
        @print_unicode 10020;
        @print_unicode 10020;
        @print_unicode 10020;
        return;
    }
#endif;
    print "???";
];

! This is an implementation of the (one-of),(or),(stopping)
! feature of I7. The routine takes an array with messages
! (string or routines) and a flag which controls if the message
! should be followed by a newline. It always return true.
!
! The messages are stored in an array table, where the first
! element is the counter which should be set to 1 initially.
! For example
!     Array example table 1 "hello" "goodbye";
!     oneof_stopping(example, true);
!
[ oneof_stopping options add_newline;
    if((options --> 1 + 1) <= options --> 0) {
        options --> 1 = 1 + options --> 1;
    }
    return oneof_print(options --> (options --> 1), add_newline);
];

! this is an implementation of the (one-of),(or),(thenrandom)
! feature of I7. See oneof_stopping for the options format
! The array may contain both string and routines,
! and the routine will always return true.
[ oneof_thenrandom options add_newline max msg;
    max = options --> 0;
    if((options --> 1 + 1) <= max) {
        options --> 1 = 1 + options --> 1;
        msg =  options --> (options --> 1);
    } else {
        ! end of list reached, return random entry
        msg = options --> (1 + random(max - 1));
    }
    return oneof_print(msg, add_newline);
];

! help routine to oneof_stopping and oneof_thenrandom
[ oneof_print msg add_newline;
    if(msg ofclass Routine) 
        msg();
    else
        print (string) msg;
    if(add_newline) @new_line;
    rtrue;
];

! Volume - The World

! Book - Generalities

! Section - Establishing Rooms And Regions

! Section - The Boring Verbs

! Section - Miscellaneous Stuff

Verb 'kiss' 'embrace' 'hug'
    * creature                                  -> Kiss;

[ KissSub;
    if (ObjectIsUntouchable(noun)) return;
    if (RunLife(noun, ##Kiss) ~= 0) rfalse;
    if (noun == player) "If you think that'll help.";
    "Keep your mind on the game.";
];

Verb 'greet'
    *   creature -> Talk;

Extend only 'say' first
    *  'hello'/'hi' 'to' creature -> Talk;

Verb 'talk'
    *   'to'/'with' creature -> Talk;

[TalkSub;
    if (noun == player) "The conversation is hardly inspiring.";
    if (RunLife(noun,##Talk) ~= false) return;
    "You have nothing to say.";
];

Extend 'look' * 'under' noun -> LookUnder;
Extend 'look' * 'through' noun -> Search;

Extend 'push' first * scenery -> Take;
Extend 'take' * multiinside 'on' noun -> Remove;

Extend 'give' first * held 'to' creature -> Show;

[ LookUnderSub;
    if(location has backstage) "There's nothing under ", (the) noun, ".";
    "You are unable to find anything beneath ", (the) noun, ".";
];

! Section - Commonalities Of Onstage Locations

Object Camera "camera"
    with
        name 'camera' 'cameras',
        found_in Hospital CraverlyManor Pizza,
        before [;
        Examine: 
            "You just accidentally glanced at a camera, but they
            can edit that out.";
        default: "You're not supposed to acknowledge it.";
        ],
    has scenery;

! Book - The Rooms Themselves

! Part - Gina's Pizzeria

Object Pizza "Gina's Pizzeria"
    with
        description [;
            "The tender light of the morning", (u8212) 0, "or the very early
            evening", (u8212) 0, " casts itself softly against the downy linen
            of spotless white tablecloths and the Italian flag hanging from
            the wall.  The exit is south.";
        ],
        before [;
        Listen:
            "All you hear is the heavy silence of an oven that has not been
            turned on, the anticipatory hush of a pizza cutter that lies
            motionless inside of an unopened drawer. You hear nothing.";
        ],
        after [;
        Look:
            if(Gina notin location) {
                if(self hasnt general) {
                    give self general;
                    "^~It looks like Gina's not here yet,~ you announce to the 
                    empty room. ~I wonder where she is.~^^The room is silent.";
                } else {
                    print "^You glance around ";
                    if(Sunglasses has worn) print "menacingly";
                    else print "meaningfully";
                    ". Gina is nowhere to be seen.";
                }
            }
        ],
        s_to EastHallway,
    has onstage;

Object TableCloth "white tablecloth" Pizza
    with
        !name 'white' 'tablecloth' 'table' 'tables' 'tableclothes',
        parse_name [ i;
            while(NextWord() == 'white' or 'tablecloth' or 'table' or 'tables' or 'tableclothes') i++;
            return i;
        ],
        description 
            "Like an expectant canvas is each tablecloth, ready to receive
            the masterpiece that is every one of Gina's pizzas.",
    has scenery;

Object Flag "flag" Pizza
    with
        name 'italian' 'flag' 'tricolor' 'tricolore',
        description
            "The tricolor stands straight and proud and wide and true for all
            to see, a symbol of the deep respect and affection that Gina
            feels toward her heritage.",
    has scenery;

! Section - Gina
 

Array ginakiss_1 table 1
    ! ONE-OF
    "~What are you doing?~ Gina asks as you approach her.^^
    ~Let me show you,~ you say, as you put your arms around her body and
    tenderly meet her lips against yours. Gina yields passionately to your
    embrace.^^But then the moment is over. ~This can never happen again,~
    Gina whispers."
    ! OR
    "You kiss Gina again. Again you feel the warmth of bodily contact that
    you have been craving for so long. Again Gina says that this can never
    happen again.";

Object Gina "Gina"
    with
        name 'gina' 'woman' 'lane' 'lady',
        initial [;
            if(self has dead) "Gina lies on the floor, dead.";
            "Gina is here, resolutely straightening things out in 
            anticipation of the day's business.";
        ],
        description [;
            if(self has dead) 
                "Gina had so much love to give this world", (u8212) 0,
                "but now she's dead. Never again will man or woman taste the
                delicious pizzas that Gina had to offer. This entepreneur has
                tossed her last crust.";
            "Gina is clearly in the midst of a valiant attempt to radiate
            her usual cheer, but her mien is marred by the looming knowledge
            of her beloved daughter's ill condition. Her pink blouse is
            wrinkled, belying the distraction that bedevils even the most 
            meticulous of worried mothers.";
        ],
        life [;
        Kiss:
            if(Sunglasses has worn) 
                "Gina rebuffs your amorous advance. ~Sidney, I wouldn't kiss
                you with my dead husband's mouth.~";
            return oneof_stopping(ginakiss_1, true);
        Talk:
            if(Sunglasses has worn) {
                return oneof_stopping(ginaspeech_2, true);
            } else if(prog == 0) {
                return oneof_stopping(ginaspeech_3, true);
            } else if(prog == 1) {
                "~Please let me know if there's any news from the hospital,~
                Gina says, trying her best not to break down into an
                avalanche of motherly tears.";
            } else if(prog == 2) {
                "~What are you still doing here?~ Gina cries.
                ~Pauline needs to know about her real father! And
                so does...her father.";
            }
        Ask:
            if(second == 'jewel' or 'gem' or 'gems' or 'jewels') {
                if(enclosedbyaroom(Satchel)) 
                    "~You got your jewels. Now, go,~ Gina pouts, her pride
                    hurt by your actions as much as her heart is hurt by
                    her daughter's illness.";
                suspect = Gina;
                return oneof_stopping(ginaspeech_1, true);
            }
        ],
        after [;
        AimAt:
            if(Sunglasses has worn)  {
                if(suspect == Gina) {
                    if(notenclosedbyaroom(Satchel)) {
                        move Satchel to player;
                        update_moved = true;
                        "^~I want those jewels, Gina!~ you say, threatening
                        the matronly entepreneur with the shining steel of
                        your lethal accessory. ~Or do you want your blood all
                        over these nice white tablecloths?~^^
                        ~Don't shoot, Sid! Here!~ She reveals a satchel, which
                        she throws across the dining room and into your hand.
                        ~Please, don't shoot me.~^^
                        ~Of course not. I wouldn't hurt you, as long as you
                        played nice.~ You shake the satchel full of jewels
                        with a satisfied air. ~And you sure did play real
                        nice,~ you say, smugly.";
                    } else {
                        "^~There are no more jewels, Sid,~ Gina cries. 
                        ~They're all in that bag.~^^
                        ~I was just making sure,~ you say.";
                    }
                } else {
                    "^Gina reels in shock at the sight of the gun that is
                    pointed her way. She cringes audibly.
                    You cackle ominously.";
                }
            } else {
                "^Gina stares down the barrel of your pistol with a face
                twisted by incredible shock.^^
                ~Doc!~ she gasps. ~Put the gun down! You're not thinking
                straight!~^^~Or am I?~ you retort.";
            }
        Shoot:
            if(self hasnt dead) {
                give self dead;
                print "Your gun screams as if in anger as it ejects its bullet
                    straight into Gina's mortal coil. If she had any last
                    words, it is too late for them now.^^She falls to the
                    floor with a horrible slump, as dead as her Italian role
                    model, Julius Caesar.^";
                if(Leopold has dead && Pauline has dead) bloodbath_ending();
                rtrue;
            }
            "You fire another bullet into Gina's dead body. ~I don't know why
            I did that,' you announce.";
        ],
    has female proper animate;

[ gina1; 
    print 
    "~You can cut the act, Gina. I know that you know that me 
    and Janine killed Wendell the dog because he knew where we buried the 
    jewels", (u8212) 0, "the jewels that you dug up in the train yard while I
    was in prison,~ you say, holding up the photo of Wendell as if it were 
    Exhibit A in a courtroom of crime.^^~Maybe that's all 
    true, or maybe not,~ Gina says, ~but I'm not handing over those 
    jewels.~^^You grimace.";
];

Array ginaspeech_1 table 1
    ! ONE-OF
    gina1
    ! OR
    "~The jewels are mine, Sid~' Gina says haughtily, her pink blouse
    rippling with satisfaction.";


Array ginaspeech_2 table 1
    ! ONE-OF
    "~Hello, Gina,~ you say, your teeth flashing impishly from
    between your wily lips.^^Gina's face is a stoic cliff, carved
    from stone. ~Sid. You're supposed to be in a federal prison.~^^
    Your cackling is like an incoming thunderstorm. ~People seem to
    think so! But I figure, wherever I am is where I'm supposed to be.~
    You smirk."
    ! OR
    "~What are you still doing here?~ asks Gina.^^
    ~Wouldn't you like to know?~ is your menacing reply.";

Array ginaspeech_3 table 1
    ! ONE-OF
    "~Pauline's insurance has run out,~ you say. Gina's expression descends
    into concern.^^
    ~She needs a procedure, or she'll die,~ you continue. ~Is there any way
    you can pay for it?~^^
    ~This pizzeria is barely paying for its own bills,~ says Gina. 
    ~All the money I could have spent to save Pauline's life, I spent
    on my dream of becoming a restauranteur.~^^
    ~We all have to live with our mistakes,~ you say, ~but Pauline may
    not have long to live with yours.~^^
    Gina tries to hide her face."
    ! OR
    "~What about Pauline's father?~ you ask. 
    ~Could he pay for the procedure?~^^
    ~Doc, Pauline's father isn't in Antibes.~ Your jaw slackens at the
    sound of Gina's revelation. ~The fact is, I don't know who Pauline's
    father is.~^^Your expression is one of shock.";

! Part - Roland Memorial Hospital

Object Hospital "Roland Memorial Hospital"
    with
        description [;
            print "The whirring and chirping of medical instruments 
                is all around", (u8212) 0, "a worrying commotion";
            if(Sunglasses hasnt worn)
                print ", to some, but to your ears it is as regular
                    as a heartbeat, as usual as business. While you walk
                    these sterile halls, you are in your element:
                    the element of health";
            ". The exit is east.";
        ],
        before [;
        Listen:
            print "The whirring and chirping of medical instruments 
                is all around. A worrying commotion";
            if(Sunglasses hasnt worn)
                print ", to some. But to your ears it is as regular
                    as a heartbeat. As usual as business";
            print ".^";
        ],
        e_to NorthHallway,
    has onstage;

Object Bed "bed" Hospital
    with
        name 'hospital' 'bed',
        description 
            "For all its convenience-enhancing technological features,
            this bed is still a detestable prison in the doe-eyes of
            its tenant, the invalid Pauline.",
    has scenery;

! Section - Pauline

Array paulinekiss_1 table 1
    ! ONE-OF
    "You plant a long, meaningful kiss on Pauline's quivering
    lips. ~It's been so long,~ she sighs."
    ! OR
    "You kiss Pauline again, for fun. Nobody tells Sidney Langridge
    not to kiss sick people.";

Array paulinekiss_2 table 1
    ! ONE-OF
    "You lean down until your face almost meets Pauline's.
    ~The Hippocratic Oath warns a doctor not to give into his or her
    feelings for a patient,~ you say, ~but an oath can only be kept for so
    long.~ Pauline rises weakly to kiss you passionately. The moment is
    perfect, but it does not last forever.^^~No one can know
    about this,~ you say. Pauline nods weakly."
    ! OR
    "You kiss Pauline again. She responds with enthusiasm despite her
    bedridden condition.";

Object Pauline "Pauline" Hospital
    with 
        name 'pauline' 'janine' 'girl' 'woman',
        initial [;
            if(self has dead)
                "Pauline's dead body lies motionless in her hospital bed.";
            print "Pauline, pale and frail, looks up at you ";
            if(Sunglasses has worn) print "confusedly"; else print "pleadingly";
            " from her supine position aboard her hospital bed.";
        ],
        description [;
            if(self has dead) 
                "No longer do Pauline's tender lips draw breath; no longer
                do her eyes plead for release from the life of infirmity
                that had entrapped her.";
            print "You gaze down ";
            if(Sunglasses has worn) print "menacingly"; else print "meaningfully";
            print " at Pauline, at her once-soft features cast into sharp
            relief by the mysterious illness that wracks her youthful form.^";
            if(self hasnt dead && self hasnt general) {
                give self general;
                @new_line;
                paulinespeak();
            }
        ],
        after [;
        Shoot:
            if(self hasnt dead) {
                give self dead;
                print "You fire into Pauline's chest. The crack of the gunshot
                    is drowned out by her desperate scream", (u8212) 0,
                    "but, an instant later, her screaming ends. She's dead.^";
                if(Leopold has dead && Gina has dead) bloodbath_ending();
                rtrue;
            }
            "You fire another bullet into Pauline's dead body. ~Now she's
            dead for sure,~ you announce.";
        AimAt:
            print "^~What are you doing?~ Pauline shrieks.^^
                ~You'll see soon enough,~ you say, ";
            if(Sunglasses has worn) print "menacingly";
            else print "meaningfully";
            ".";
        ],
        life [;
        Talk: paulinespeak(); rtrue;
        Kiss:
            if(Sunglasses has worn) {
                return oneof_stopping(paulinekiss_1, true);
            } else {
                return oneof_stopping(paulinekiss_2, true);
            }
        ], 
    has female proper animate;

Array paulinespeak_1 table 1
    ! ONE-OF
    "~Sidney!~ Pauline's eyes widen as they detect your
    identity. ~You shouldn't be here! What if the doctor, your twin,
    sees you?~^^~You leave the nerd to me, sugarbabe.
    I've been dealing with that clamshell since the day we were
    both born.~"
    ! OR
    "~You came back for me, Sidney,~ Pauline says, her chest working
    double-time to force the words out of her lungs.^^
    ~I sure did, sugarbabe.~";

Array paulinespeak_2 table 1
    ! ONE-OF
    "~Oh, Doctor Langridge,~ Pauline rasps, ~Tell me some good
    news.~^^~I wish I could,~ you say, ~but the truth
    is that your condition is getting worse. And the even worse news
    is that your insurance won't pay for your procedure.~^^
    Pauline's lip quivers."
    ! OR
    "~Can't anything be done, Doctor Langridge?~ Pauline asks
    pitifully.^^~I'll talk to your mother,~ you say,
    though your countenance does not indicate confidence.";

[ paulinespeak;
    if(Sunglasses has worn) {
        oneof_stopping(paulinespeak_1, true);
    } else if(prog == 0) {
        oneof_stopping(paulinespeak_2, true);
    } else if(prog == 1) {
        print  "~Please let me know if you find out about those test
            results,~ Pauline pleads.^";
    } else if(prog == 2) {
        print "Pauline looks up at you, beautiful tears welling in her still
            more beautiful eyes. ~Please, Doctor Langridge! Talk to my father!
            He might be able to help!~^";
    }
];

! Part - Craverly Manor

Object CraverlyManor "Craverly Manor"
    with
        description
            "A gargantuan portrait of the hawk-eyed and hawk-nosed 
            Leopold Craverly stares down at you from its perch on the
            mahogany-panelled wall. The exit is north.",
        before [;
        Listen:
            "The manor is eerily quiet. The other Craverlies have left;
            only Leopold lives here now.";
        ],
        n_to WestHallway,
    has onstage;

Object Portrait "gargantuan portrait" CraverlyManor
    with
        name 'painting' 'picture' 'gargantuan' 'portrait',
        description 
            "The portrait may resemble Craverly even more  than does
            Craverly himself. Art imitates life, yes: but life is messy, 
            and inaccurate; art is perfect, and reflects the real world
            as it really is.",
    has scenery;

Object Wall "mahogany-panelled wall" CraverlyManor
    with
        !name 'walls' 'mahogany' 'wood' 'panel' 'panels' 'mahogany-panelled' 'wall',
        parse_name [ i;
            while(NextWord() == 'mahogany-panelled' or 'wall' or 'walls' or 'mahogany' or 'wood' or 'panel' or 'panels') i++;
            return i;
        ],
        description
            "These stoic walls have enclosed the Craverly clan for generations, 
            ever since Aloysius Craverly built Craverly Manor and founded
            Craverly Heights in the early nineteenth century.",
    has scenery;
 
! Section - Leopold Craverly
 
[ sidenote; 
    if(suspect == Leopold) {
        if(Sunglasses has worn) {
            if(enclosedbyaroom(Satchel)) {
                print ", and you will never find the jewels";
            }
        }
    }
];

[ leo2;
    print
    "You rush boldly up to Leopold and, before he can react, 
    you softly grab his head and press your lips against his lips. The 
    meeting of your bodies is an explosive event, but it is not accompanied 
    by a meeting of hearts.^^
    ~Oh, Langridge,~ Leopold mutters, still embraced by your arms, 
    ~I left my chances for love behind a long time ago.
    Don't turn out like me", (u8212) 0,
    "don't hold out hope for something that can never be.~^^
    With an expression of tremendous emotion, you step away from him.";
];

Array leopoldkiss_1 table 1
    ! ONE-OF
    leo2
    ! OR
    "Leopold allows you to kiss him once more, but you come no closer to
    melting the diamond cage that surrounds his paralytic soul.";

Array leopoldspeak_1 table 1
    ! ONE-OF
    "~Hello, there, Leo!~ you say.^^
    ~Out of prison again,~ Leopold begins, and you say ~Naturally!~ as
    he says ~I see.~ You both stop talking, and Leopold coughs, waiting
    for you to talk to him again."
    ! OR
    "~Hello, there, Leo!~ you say.^^
    ~Out of prison again,~ Leopold says, and then he is quiet for a moment.^^
    ~Naturally!~ you say, too late, and Leopold shakes his head. 
    ~Sorry. Let's try it one more time.~^^
    You shuffle your feet."
    ! OR
    "~Hello, there, Leo!~ you say.^^
    ~Out of prison again,~ Leopold says, and you say ~Naturally!~^^
    Leopold nods.";

[ leo1;
    print 
    "~I beg you to reconsider Janine's plight, Mister Craverly,~ you say.^^
    ~I think you mean ", (italic_type) 0, "Pauline", (roman_type) 0, 
    ". And I beg you to leave off the subject,
    Langridge!~ answers Leopold in a mocking tone.";
];

Array leopoldspeak_2 table 1
    ! ONE-OF
    "~What's new over at Roland Memorial, Langridge?~ Leopold asks.^^
    ~It's Gina's daughter, Pauline. They can't afford the only procedure
    that will keep her alive. But, Mister Craverly, I know you are not an
    uncharitable man.~^^
    Leopold lets loose a torrent of guffaws. ~I think that sentence needs
    one more negative, Langridge! Of what concern to me is the situation
    of a girl I wouldn't recognize on the street?~^^
    Your response to this question is a glare like a polished silver dagger."
    ! OR
    leo1;

Array leopoldask_1 table 1
    ! ONE-OF
    "~All right, Leo. We both know that I made Janine hit 
    Wendell with her car after he you saw him digging down at the train yard
    where we buried the jewels. I mean, Pauline.~^^
    Craverly chortles. ~You think I have the jewels? Well, even if I
    do, they're staying with me.~"
    ! OR
    "Craverly laughs again. ~I won't give you the jewels, 
    no matter how many times you ask me about them.~";

[ leospeak;
    if(Sunglasses has worn) return oneof_stopping(leopoldspeak_1, true);
    oneof_stopping(leopoldspeak_2, true);
];

Object Leopold "Leopold Craverly" CraverlyManor
    with
        name 'leopold' 'man' 'leo' 'craverly',
        initial [;
            if(self has dead)
                "Leopold lies dead on the floor, his stiffening hand still
                clutching at the place in his chest where you shot him
                through the heart.";
            "Standing beneath the portrait is Leopold Craverly himself,
            identical to his own image in every way. He eyes you sternly
            from his perch atop his snake-headed cane.";
        ],
        description [;
            if(self has dead)
                "Leopold thought he would live long enough to see all of his
                dreams come true. Now you know that his belief was false.";
            print "Though his hair is silver-white, and his eyes have been
                furrowed by a lifetime of avarice and cynicism, Leopold still
                has many years left in his life, and many goals left to achieve
                in those years, no matter what or who stands in his way.^";
            if(self hasnt dead && self hasnt general) {
                give self general;
                @new_line;
                leospeak();
            }
        ],
        life [;
        Kiss:
            if(Sunglasses has worn) 
                "Leopold pushes you away. ~Forget it, Sid. I'm not one
                of those young people who melts in half when a criminal
                comes on to him.~";
            return oneof_stopping(leopoldkiss_1, true);
        Talk:
            leospeak(); rtrue;
        Ask:
            if(second == 'jewel' or 'gem' or 'gems' or 'jewels') {
                if(Sunglasses has worn && suspect ~= Gina) {
                    suspect = Leopold;
                    return oneof_stopping(leopoldask_1, true);
                }
            }
        ], 
        before [;
        Shoot:
            if(self hasnt dead) {
                give self dead;
                print "~Come now, ";
                if(Sunglasses has worn) {
                    sidshotleo = true;
                    print "Sidney";
                } else {
                    print "Langridge";
                }
                print ", calm yourself!~ the old man cries, but it is too late
                    for him. You fire the gun into his heart, the heart that
                    could never bring itself to feel love. It will never feel
                    anything again";
                sidenote();
                print ".^^Craverly falls to the floor, dead.^";
                if(Pauline has dead && Gina has dead) bloodbath_ending();
                rtrue;
            } else {
                "You fire another bullet into Leopold Craverly's dead body.
                ~That one was for Wendell,~ you say.";
            }
        ],
        after [;
        AimAt:
            if(Sunglasses has worn && suspect == Leopold) {
                if(notenclosedbyaroom(Satchel)) {
                    move Satchel to player;
                    update_moved = true;
                    "^~I want those jewels, old man!~ you say, menacing
                    the silver-haired millionaire with the gleaming
                    barrel of your murderous weapon. ~Are you going to
                    play along, or are you going to die?~^^
                    ~All right, all right! Here!~ Leopold draws a 
                    satchel out of his pocket and tosses it to you.
                    ~They're all there. Just leave me be!~^^
                    You feel the heft of the satchel in your hand and
                    smile a smug smile.";
                } else {
                    "^~I've already given you the jewels, Sidney,~ 
                    Leopold chides.^^
                    ~That's right, you did,~ you respond.
                    ~And don't you forget it!~";
                }
            } else {
                print "^Leopold regards the handgun with a cool
                    indifference. ~Put that thing down, ";
                if(Sunglasses has worn) print "Sidney"; else print "Langridge";
                ". You're liable to hurt yourself.~";
            }
        ],
    has proper animate;

Object Cane "snake-headed cane" Leopold
    with
        name 'cane' 'snake' 'head' 'snake-headed',
        description [;
            print "A mystery: Does Leopold carry this sinister cane because
                of a physical infirmity, or is its purpose merely to inspire 
                apprehension and respect in those who behold it? Even you, ";
            if(Sunglasses has worn) print "a master criminal";
            else print "the finest doctor in Craverly Heights";
            ", cannot be sure.";
        ];

! Part - The Hallways
 
! Chapter - North Hallway

Array hallwaystring_1 table 1 "Pauline's" "Pauline's" "Janine's" "Pauline's";

Object NorthHallway "North Hallway"
    with
        description [;
            oneof_thenrandom(hallwaystring_1);
            " hospital room is west from here. The hallway continues south.";
       ],
       w_to Hospital,
       s_to HallwayIntersection,
    has backstage;

Object Shelves "rack of shelves" NorthHallway
    with
        name 'shelf' 'shelves' 'rack' 'of',
        initial 
           "There's a rack of shelves standing against this end of the hall.",
        description [;
            <Search self>;
        ],
        before [;
        Take: print_ret "That belongs where it is.";
        ],
    has supporter open pluralname;

! Section - The Magnifying Glass
 
Object MagnifyingGlass "magnifying glass" Shelves
    with
        name 'magnifying' 'glass',
        description [;
            if(location has backstage) {
                "A cheap, beat-up magnifying glass.";
            }
            if(Sunglasses has worn) {
                "As a criminal mastermind, you have no need for
                magnifying glasses. What concerns you is the big picture.";
            }
            "Every doctor needs a magnifying glass, the better with which
            to see the minuscule problems that plague humanity in such 
            enormous ways.";
        ],
        before [foo;
        Search:
            if(location has backstage) 
                "The glass fails to reveal any new information.";
            ! let foo be a random scenery in the location;
            ! There are only three onstage room, let's hardcode instead
            if(location == Hospital) foo = random(Bed, Camera);
            if(location == Pizza) foo = random(TableCloth, Flag, Camera);
            if(location == CraverlyManor) foo = random(Portrait, Wall, Camera);
            if(foo == Camera) {
                "You lean in for a closer look at nothing in particular.";
            } else {
                "You lean in for a closer look at ", (the) foo, ".";
            }
        Show:
            if(second == Lane) {
                "Lane shakes her head. ~It's a piece of junk.~";
            }
            if(second == Leopold) {
                print "Leopold scoffs at your proffered magnifying glass. 
                    ~I'm afraid I don't have time for your ";
                if(Sunglasses has worn) 
                    print "Nancy Drew nincompoopery, Sidney";
                else
                    print "Hardy Boys shenanigans, Langridge";
                ".~";
            }
            if(second == Gina) {
                print "~Are you on the trail of a mystery?~ Gina asks.^^
                    ~In my own way, I suppose, I am,~ you say, ";
                if(Sunglasses has worn) print "menacingly";
                else print "meaningfully";
                ".";
            }
            if(second == Pauline) {
                print "As Pauline looks through the magnifying glass, an
                    expression of befuddlement overtakes her face. ~Everything
                    is distorted and strange,~ she says.^^~That's ";

                if(Sunglasses has worn) 
                    print "because magnifying glasses are for sucks";
                else
                    print "in the nature of a lens,~ you respond. ~They can
                    reveal to us the world's secret truths, or they can show
                    us a bizarre parody of our lives, as if reflected in a
                    funhouse mirror. We must always be mindful of this, when
                    we gaze through a glass";
                ",~ you say.";
            }
        ];

! Section - The Framed Photo
 
Array photo_leopold_1 table 1
    ! ONE-OF 
    "~You remember Wendell, right?~^^
    ~Of course,~ Craverly sneers, ~That's Gina's dog. I seem to recall it
    perished under mysterious circumstances?~^^
    ~Cut the bull, Leo. We both know that I made Janine hit Wendell with
    her car after you saw him digging down at the train yard where we buried
    the jewels. I mean, Pauline.~^^
    Craverly chortles. ~And now you think I have the jewels? Well, even if
    I do, they're staying with me."
    ! OR
    "Craverly laughs again. ~I won't give you the jewels, no matter how many
    times you show me that photograph.~";

Array photo_pauline_1 table 1
    ! ONE-OF 
    "Salty, unhappy tears well up in Pauline's eyes as she regards the
    image of her beloved dog. ~I miss him so much,~ she says.^^
    ~But you remember why we killed him, don't you?~ you ask.^^
    ~Of course. Wendell kept trying to dig up the place where we buried
    the jewels.~^^
    ~Yeah, well, right after I got out of prison I went back to that
    place, where we buried the jewels. But someone got there first.
    Someone dug them up.~^^
    Pauline gasps. ~But who else knew about the jewels?~^^
    ~Only one person,~ you menacingly say."
    ! OR
    "~Have you talked to the one other person who knew about the jewels
    yet?~ Pauline asks.";

Array photo_pauline_2 table 1
    ! ONE-OF 
    "At the sight of her beloved departed dog, Pauline's 
    expression sinks still further into the mire of melancholy.^^
    ~Oh, Wendell,~ she mourns, on the verge of weeping. ~If only you 
    were here, I wouldn't feel nearly so awful. If I ever find out who took 
    you from me...~^^
    ~But Wendell's death was ruled an accident,~ you say.^^
    Pauline fixes you with as steely a glare as she can muster in her
    depleted state. ~You don't believe that, do you?~ she asks.^^
    ~No,~ you say."
    ! OR
    "You offer the photo to Pauline again, but she refuses to look at it.";

Object FramedPhoto "framed photo" Shelves
    with
        !name 'wendell' 'photo' 'framed' 'picture' 'photograph' 'dog',
        parse_name [ i;
            while(NextWord() == 'wendell' or 'photo' or 'framed' or 'picture' or 'photograph' or 'dog') i++;
            return i;
        ],
        description [;
            if(location has backstage) {
                "In the frame is a photo of a dog.";
            }
            if(Sunglasses has worn) {
                "In this picture frame is a photo of Wendell the dog,
                Pauline's dog, which she loved. But you know that Pauline
                has a secret, a secret concerning Wendell.";
            }
            "A photo of Pauline's beloved golden retriever, Wendell. A
            faithful companion to his owner, and beloved of all citizens
            of Craverly Heights, Wendell was cut down before his time",
            (u8212) 0, "under mysterious circumstances.";
        ],
        before [;
        Show:
            if(second == Lane) {
                "Lane squints at the photo of the dog. ~I'm pretty
                sure this came with the frame.~";
            }
            if(second == Pauline) {
                if(Sunglasses has worn) {
                    return oneof_stopping(photo_pauline_1, true);
                }
                return oneof_stopping(photo_pauline_2, true);
            }
            if(second == Gina) {
                if(Sunglasses has worn) {
                    if(enclosedbyaroom(Satchel)) {
                        "~That dog's death was as meaningless as your life,~
                        Gina eloquently states.";
                    } else if(suspect ~= Leopold) {
                        suspect = Gina;
                        return oneof_stopping(ginaspeech_1, true);
                    } else {
                        "~Where did you get that photo of my daughter's
                        beloved golden retriever Wendell?~ Gina asks
                        indignantly and accusingly. You lick your lips
                        menacingly.^^
                        ~So it's a crime to have a photo of a dog now?~
                        you ask. ~What is this world coming to?~^^
                        Gina glares at you.";
                    }
                } else {
                    "Gina regards the photo sadly. ~Oh, Wendell, you big lug.
                    Why'd you have to leave us?~ she asks.^^
                    ~You don't have any clues to the mystery of his death?~
                    you ask.^^
                    Gina regards you angrily. ~Are you accusing me of
                    something?~ she asks.^^
                    ~Of course not,~ you answer.";
                }
            }
            if(second == Leopold) {
                if(Sunglasses has worn) {
                    if(enclosedbyaroom(Satchel)) {
                        "Leopold sneers. 'You wouldn't stay in prison, but at
                        least that dog will stay in the ground.'";
                    } else if(suspect ~= Gina) {
                        suspect = Leopold;
                        return oneof_stopping(photo_leopold_1, true);
                    } else {
                        "~Why are you showing me this dog?~ Craverly asks, his
                        question as pointed as the tip of his snake-headed
                        cane.^^~This dog is dead,' you say, ~and I'm going to
                        get the jewels it found.~^^
                        ~Does this concern me in any way?~ asks Craverly, his
                        question as tentative as his grasp of the concept of 
                        empathy.^^~Not for now, it doesn't,~ 
                        you reply, menacingly.";
                    }
                } else {
                    "~What's that there?~ Craverly asks.^^
                    ~It's Wendell,~ you say. ~Pauline's dog.~^^
                    ~Ah, I see, I see. And how has he been doing?~^^
                    ~He died three years ago,~ you answer meaningfully,
                    ~Under mysterious circumstances.~^^
                    Craverly stares meaningfully back at you.";
                }
            }
        ],
    ;
 
! Section - The Pair of Sunglasses
 
Object Sunglasses "sunglasses" Shelves
    with 
        !name 'sunglasses' 'glasses' 'shades' 'glass' 'shade' 'pair' 'of' 'them',
        parse_name [ i;
            while(NextWord() == 'sunglasses' or 'glasses' or 'shades' or 'glass' or 'shade' or 'pair' or 'of' or 'them') i++;
            return i;
        ],
        article "a pair of",
        description [;
            if(location has backstage) {
                "Grossly out-of-fashion, and as flimsy.";
            }
            if(self has worn) {
                "Your sunglasses are what set you apart from your
                twin", (u8212) 0, "that, and the fact that you flunked out of
                medical school.";
            }
            "With a pang of familial familiarity, these flat-topped shades
            remind you vividly of the sunglasses that your twin Sid used
            to wear.";
        ],
        before [;
        Disrobe, Wear:
            if(location has onstage) {
                "This is not a suitable venue for you to be altering your 
                appearance so drastically and cavalierly.";
            }
        ],
    has clothing pluralname;

! Section - The Handgun
 
Object Handgun "handgun" Shelves
    with
        !name 'handgun' 'gun' 'pistol' 'firearm' 'trigger',
        parse_name [ i;
            while(NextWord() == 'handgun' or 'gun' or 'pistol' or 'firearm' or 'trigger') i++;
            return i;
        ],
        description [;
            if(location has backstage) {
                "Nickel-plated. Shiny.";
            }
            if(Sunglasses has worn) {
                "The familiar weight of your gun in your hand is like a
                soft peck on the cheek from your mother", (u8212) 0, 
                "you assume. Mom always liked your twin better.";
            }
            "How ironic that a healer and caregiver such as yourself should
            carry in your hands such a monstrous implement of destruction.
            Or is it? Maybe it is not ironic at all. Maybe it makes perfect
            sense for you to carry this gun: you, who so often hold the
            powers of life and death in your hands.";
        ],
        before [;
        Show:
            if(second == Gina) {
                if(Sunglasses has worn)
                    "~Is that the gun you used to shoot the mayor?~ Gina asks.^^
                    ~Nah, this gun is innocent,~ you say, quickly adding:
                    ~And so am I!~";
                "Gina regards your gun with a skeptic's pair of eyes. ~Why
                would a doctor need a gun?~ she asks.^^
                ~We doctors need a lot of things,~ you answer meaningfully.";
            }
            if(second == Leopold) {
                "~A cute toy,~ Craverly says, clearly unimpressed by the
                instrument of death you present to him. ~Be careful with it;
                you might break a window!~ He guffaws enthusiastically at
                his own flimsy joke.";
            }
            if(second == Pauline) {
                if(Sunglasses has worn)
                    "~Nice gun,~ Pauline says.^^
                    ~Thanks,~ you say, menacingly.";
                "~Why would a doctor need a gun?~ Pauline asks, her eyes
                clouded with innocence.^^
                ~That's a good question, Pauline. But I can't tell you,~
                you say. And then you add: ~Yet.~";
            }
            if(second == Lane) {
                "Lane shrugs. ~I've seen cooler.~";
            }
        Touch:
            if(location has backstage) {
                "It feels a lot like plastic.";
            }
            "The steel of the gun feels cold. Cold like a broken promise.";
        ];


Global target;

Object FauxThing "faux";

[ updatetarget;
    ! The game will remember what you're pointing at for one turn,
    !and then (unless you get on with it and shoot the thing)
    !it won't care anymore.
    if(action == ##AimAt) {
        target = noun;
    } else {
        target = FauxThing;
    }
];

[ enclosedbyaroom obj p;
    p = parent(obj);
    return (p ~= 0 && p has onstage or backstage);
];

[ notenclosedbyaroom obj;
    !rtrue; ! I7 bug before 61.02
    return ~enclosedbyaroom(obj);
];

Verb 'aim' 'point'
    * noun -> Aim
    * held 'at' noun -> AimAt reverse;

[ AimSub;
    print "You point ", (the) noun;
    if(location has onstage) print " squarely";
    " at nothing in particular.";
];

[ AimAtSub;
    if(noun == player && second == Handgun) {
        if(location has backstage) {
            "You point the gun at yourself. Kind of scary. Not really.";
        } else {
            print "You raise the gun to your temple";
            if(Pauline in location) print ". Pauline gasps";
            if(Gina in location) print ". Gina gasps";
            if(Leopold in location) print ". Leopold squints at you";
            ".";
        }
    }
    print "You point ", (the) second;
    if(location has onstage) print " squarely";
    print " at ", (the) noun, ".^";
    if(second == Handgun) if(AfterRoutines()) rtrue;
];

Verb 'shoot' 'fire'
    * -> ShootVague
    * noun -> Shoot
    * 'at' noun -> Shoot;

[ ShootVagueSub;
    if(Handgun in player) <<Shoot Handgun>>;
    "You don't have anything with which to shoot.";
];

[ ShootSub;
    if(Handgun notin player) 
        "You don't have anything with which to shoot.";
    if(noun == Handgun) {
        if(target == FauxThing) {
            if(location has backstage) 
 			    "You pull the trigger. Nothing. It doesn't even click.";
 			else 
 			    "With an expression of utter animalism on your face,
 			    you raise your gun and fire a shot into the air." ;
        } else {
            <<Shoot target>>;
        }
    }
    if(AfterRoutines()) rtrue;
    if(location has backstage) {
        "The gun has no effect on ", (the) noun, ". The trigger
        doesn't even click.";
    }
    print "You stop yourself. ~This ", (name) noun,
        " isn't worth my bullets,~ you say, ";
    if(Sunglasses has worn) {
        "menacingly. ~Bullets are expensive.~^^
        You squint menacingly at ", (the) noun, ".";
    } else {
        "thoughtfully. ~It's done nothing wrong.  Some things in
        this world are still innocent. But some aren't. But even
        if they are, is it my duty to mete out justice?~ You look
        down meaningfully at the gun.";
    }
];

[ pressanykey;
    print "^PRESS ANY KEY TO CONTINUE^^";
    _ReadPlayerInput(true);
    print (u10020) 0, "^^";
];

[ bloodbath_ending;
    deadflag = GS_DEATHMESSAGE;
    finalmessage = "Tune in next week!";
    pressanykey();
    "~So what happens now?~^^
    ~First of all, none of that happened.~^^
    ~You mean like, we're gonna say it was a dream?~^^
    ~No, I mean we're going to scrap all of it and start from scratch.
    Nobody had a dream, nobody got shot, nothing. We're going to air a
    rerun. Then we're going to hire a new writer, and we're going to do a ",
    (italic_type) 0, "real", (roman_type) 0, " episode, with a plot. And ",
    (italic_type) 0, "you", (roman_type) 0, " are forbidden from improvising.
    Never again.~";
];

[ attempto deadnpc i w;
    if(consult_from == 0 && noun ~= 0 && noun has animate && noun has dead) deadnpc = noun;
    if(consult_from == 0 && second ~= 0 && second has animate && second has dead) deadnpc = second;
    if(deadnpc ~= 0 && action ~= ##Examine) {
        print "~I wish... I wish I could";
        for(i = 0 : i < parse -> 1 : i++) {
            w = (parse + 2 + i * 4) --> 0;
            if(w == 'pauline') print " Pauline";
            else if(w == 'janine') print " Janine";
            else if(w == 'leopold') print " Leopold";
            else if(w == 'wendell') print " Wendell";
            else if(w == 'gina') print " Gina";
            else if(w == 'craverly') print " Craverly";
            else print " ", (address) w;
        }
        print ", but it's too late,~ you announce, your voice weighed by
            the burden of monstrous regret brought on by the enormity of
            the fact that you have brought from the realm of the
            unspeakable into the realm of the true: ~";
        if(noun has female) print "She"; else print "He";
        "'s dead.~";
    }
    rfalse;
];
 
! Chapter - East Hallway
 
Object EastHallway "East Hallway"
    with
        description
            "Gina's Pizzeria is north from here. 
            The hallway continues west.",
        n_to Pizza,
        w_to HallwayIntersection,
    has backstage;

! Chapter - South Hallway

Object SouthHallway "South Hallway"
    with
        description
            "The break room is east from here. 
            The hallway continues north.",
        e_to Breakroom,
        n_to HallwayIntersection,
    has backstage;

! Chapter - West Hallway

Object WestHallway "West Hallway"
    with
        description
            "Craverly Manor is south from here. 
            The hallway continues east.",
        s_to CraverlyManor,
        e_to HallwayIntersection,
    has backstage;
 
! Chapter - Hallway Intersection
 
Object HallwayIntersection "Hallway Intersection"
    with
        description
            "Nobody's around.^^
            The hallway runs north, south, east, and west from here.",
        n_to NorthHallway,
        s_to SouthHallway,
        e_to EastHallway,
        w_to WestHallway,
    has backstage;

Object Bulletin "non-diegetic bulletin" HallwayIntersection
    with
        name 'non-diegetic' 'bulletin',
        initial
            "A non-diegetic bulletin is posted on the wall.",
        description [;
            "The bulletin reads: ", (italic_type) 0, 
            "Hey! You, with the keyboard! Let me know 
            what you think of this game on Twitter (@@64rcveeder) or via
            email (rcveeder@@64me.com).^^Here's a tip: To finish the game, 
            you're going to have to use the verb SHOW, as in 
            ~>show THING to PERSON~. You might also have to 
            ~>point THING at PERSON~ at some point.^^
            Thanks for playing.", (roman_type) 0;
        ],
        before [;
        Examine: rfalse;
        default: "You're not supposed to pay too much attention to that.";
        ],
    has static;

! Part - Break Room
 
Object Breakroom "Break Room"
    with
        description
            "An antique vending machine takes up most of 
            the tiny room. The exit is west.",
        w_to SouthHallway,
    has backstage;

Object VendingMachine "vending machine" Breakroom
    with
        !name 'vending' 'machine' 'note' 'notes' 'sticky',
        parse_name [ i;
            while(NextWord() == 'vending' or 'machine' or 'note' or 'notes' or 'sticky') i++;
            return i;
        ],
        description 
            "There's one sticky note on the machine that says
            ~OUT OF ORDER~, and there's another sticky note that
            says ~THIS MACHINE OWES ME $7000~.",
        before [;
        Take: "You can't lug this vending machine around. It's the real thing.";
        ],
    has scenery;

Global table_cnt;
[ CheckTableContents obj;
    if(obj in Table) table_cnt++;
];

Object Table "table" Breakroom
    with
        name 'table',
        describe [;
            table_cnt = 0;
            LoopOverScope(CheckTableContents);
            if(table_cnt > 0) {
                @new_line;
                <Search self>;
            }
            rtrue;
        ],
        description"It's a piece of junk.",
    has static supporter;
 
! Section - The Script

Array script_1 table 1
    ! ONE-OF
    "~I've finished analyzing your results,~ you say, passing the papers
    to Pauline. She inspects them closely.^^
    ~As you can see, the tests don't apply to you specifically. They also
    have something to do with the identity of your father.~^^
    Pauline looks up from the paper, her eyes wide with amazement.^^
    ~Now, I need those back, for reasons I think you can guess.~ You pull
    the test results out of her hands."
    ! OR
    "Pauline frowns. ~I've seen the results, Doctor Langridge,~ she says,
    ~But have you shown them to...my father?~^^
    ~I just wanted to make sure that's what you wanted,' you reply.^^
    ~It is,~ says Pauline.";

Object Script "script" Table
    with
        !name 'script' 'sheet' 'sheets' 'paper' 'papers' 'test' 'result' 'results' 
        parse_name [ i;
            while(NextWord() == 'script' or 'sheet' or 'sheets' or 'paper' or 'papers' or 'test' or 'result' or 'results') i++;
            return i;
        ],
        short_name [;
            if(location has backstage) {
                print "script";
            } else if(prog == 0) {
                print "couple sheets of paper";
            } else {
                print "set of test results";
            }
            rtrue;
        ],
        before [;
        Show:
            if(second == Lane) {
                "Lane has already seen the script. She rolls her eyes.
                ~I know, right?~";
            } else if(second == Leopold)  {
                if(Sunglasses has worn) {
                    "~What's that you've got there?~ Leopold asks.^^
                    You smirk with unconscionable smugness. ~It's a script.
                    I'm gonna be a star,~ you say.^^
                    ~I imagine you must think so. But you're no star,
                    Sidney. You're a black hole.~^^
                    You grimace at the disquieting veracity of
                    Leopold's words.";
                } else if(prog == 0) {
                    "~What's this?~ Leopold says, eyeing the papers with
                    inexplicable contempt. ~Some document you found at the
                    hospital?~^^
                    ~Yes,~ you say.^^
                    ~Then I can safely assume that it does not concern me!~
                    Craverly punctuates his statement with a
                    triumphant guffaw.";
                } else if(prog == 1) {
                    "~What's this?~ Leopold asks, eyeing the papers with
                    inexplicable contempt.^^
                    ~These are test results,~ you say.
                    ~Pauline's test results.~
                    ^^~Ah! I will attempt an impersonation of someone who
                    cares, then: What do they say?~^^
                    You glare at the old man, at his hateful face.
                    ~I haven't figured that out. Yet.~^^
                    ~Of course! I expect nothing more from the  greatest
                    doctor in Craverly Heights.~ Leopold guffaws.";
                } else if(prog == 2) {
                    deadflag = GS_DEATHMESSAGE;
                    finalmessage = "We gotta hire a flippin' writer.";
                    print "~What's this?~ Leopold asks, eyeing the papers
                        with inexplicable trepidation.^^
                        ~These are test results. Pauline's test results,~ you
                        say, extremely meaningfully. ~They constitute
                        conclusive proof that you are Pauline's father.~^^
                        The weight of this revelation nearly knocks Leopold
                        onto the floor. He grasps his cane all the tighter,
                        and mutters:^^
                        ~Nineteen years ago... The train tunnel collapse!~^^
                        ~The underground gases that seeped into the tunnel
                        caused amnesia in all of the passengers,~ you explain.
                        ~Nobody on the train could remember what happened before
                        the rescue team arrived. But now we know about one thing
                        that definitely did happen. You and Gina conceived a
                        child, a child that desperately needs a medical
                        procedure she can't afford.~
                        ^^~I'll pay every cent!~ Leopold is on the verge of
                        tears. ~I have a daughter! The Craverly line will
                        go on!~^";
                    pressanykey();
                    print "~And fade to black. See?
                        Everything worked out fine.~^^";
                    if(Gina has dead || Pauline has dead) {
                        print "~But ";
                        if(Gina has dead && Pauline has dead) {
                            print "Pauline and Gina are";
                        } else if(Gina has dead) {
                            print "Gina is";
                        } else if(Pauline has dead) {
                            print "Pauline is";
                        }
                        print " dead.~^^
                            ~Nah, we can edit that out. What the heck were 
                            you trying to accomplish there?~^^
                            An answer does not spring to mind.^^";
                    }
                    "~So Pauline's gonna be okay?~^^
                    ~Of course! We were never gonna kill her off.
                    She has to stay in that bed until Lisa has her baby,
                    but we'll figure that out.~^^
                    The director leans back and sighs. ~Now, you guys did
                    a good job out there today. I don't want to diminish
                    the enormity of your accomplishment. But before we get
                    working on #6002, we gotta get one thing done.~";
                }
            }
            if(second == Gina) {
                if(Sunglasses has worn) {
                    "~This is the deed to my new condo,~ you tell Gina. She
                    cranes her head to get a better look at the mysterious
                    papers while maintaining a safe distance from your
                    crime-ridden form.^^
                    ~I hope it's far away from here,~ she says.^^
                    ~Nice try,~ you say, ~But I'll never tell the likes of
                    you where it is!~";
                } else if(prog == 0) {
                    "Gina is utterly boggled by the papers you present to her.
                    ~I have no idea what this is,~ she says. ~Did you get this
                    back at the hospital?~^^
                    ~Yes, Gina,~ you say. Of course you did.^^
                    ~Then maybe they'll hold some interest to Pauline. If
                    she has the strength to read them,~ Gina adds bitterly.";
                } else if(prog == 1) {
                    if(Leopold has dead) {
                        deadflag = GS_DEATHMESSAGE;
                        finalmessage = "Tune in next week!";
                        print "~I wanted to show you these test results,~ you
                            say, thrusting the papers in to Gina's motherly
                            hands. She looks them over with an expression that
                            changes from skeptical, to interested, to
                            astonished.^^
                            ~Doc! This means...~^^
                            You lean in closer.^^
                            ~This means...~ Gina stammers, ~Pauline's father
                            is Leopold Craverly!~^^
                            Your eyes widen. ~But I just ";
                        if(sidshotleo == true) 
                            print (u8212) 0,"I mean, Sidney, my twin, just ";
                        print "murdered him!~^^";
                        pressanykey();
                        "~And why did you do that?'^^
                        ~I don't know! We had no script!~^^
                        ~So you murdered your costar? I thought the first rule
                        of improv was 'Don't randomly kill your fellow
                        characters'.'^^
                        ~Actually, the first rule of improv is, you're 
                        supposed to say '~Yes, and'.~^^
                        ~Oh, I see. Let me try it out: Yes, and,
                        you're fired.'";
                    } else {
                        prog = 2;
                        "~I wanted to show you these test results,~ you say,
                        thrusting the papers in to Gina's motherly hands. She
                        looks them over with an expression that changes from
                        skeptical, to interested, to astonished.^^
                        ~Doc! This means...~^^
                        You lean in closer.^^
                        ~This means...~ Gina stammers,
                        ~Pauline's father is...~^^
                        Your eyes widen. Of course!";
                    }
                } else if(prog == 2) {
                    "~I don't need these!~ exclaims Gina. ~Pauline needs to
                    find out who her real father is! And so does...her father.~";
                }
            }
            if(second == Pauline)  {
                if(Sunglasses has worn) {
                    "~What's that?~ Pauline asks.^^
                    You shake the papers triumphantly. ~This is my
                    certificate of release! From prison! They give you a
                    certificate that says you aren't supposed to be
                    incarcerated anymore, in case a cop tries to toss you
                    back in the slammer.~^^
                    ~I didn't know that,~ says Pauline.";
                } else if(prog == 0) {
                    prog = 1;
                    "You place the papers gingerly in Pauline's
                    delicate hands.^^
                    ~Are these...the test results?~ Pauline asks.^^
                    ~Yes, Janine,~ you say. Of course they are.^^
                    ~What do they say?~^^
                    You bite your lip thoughtfully. ~I still need to analyze
                    them,~ you say. ~I'll let you know once I'm finished.~";
                } else if(prog == 1) {
                    "~Have you finished analyzing the test results?~
                    Pauline asks. You frown.^^
                    ~I'm afraid not. Soon, though.~ You hope.^^
                    ~Maybe my mother can help,~ says Pauline.^^
                    ~Very good idea, Janine. I'll see what she has to say.~ 
                    You cough. ~I mean, Pauline.~";
                } else if(prog == 2) {
                    return oneof_stopping(script_1, true);
                } 
            }
        Examine:
            if(location has backstage) {
                print "Two pages. The first page reads:^^";
                print (fixed_letter_spacing) 0, 
                    "CRAVERLY HEIGHTS^EPISODE # 6001^^
                    If it's so easy, why don't you write it yourself?^^",
                    (variable_letter_spacing) 0;
                font on;
                "The second page is blank.";
            }
        ];

! Section - Lane

Object Lane "Lane" Breakroom
    with
        name 'lane',
        initial "Lane is sitting around with a blank look on her face.",
        description "Lane is wearing a pink blouse.",
        before [;
        Talk:
            remove Lane;
            move Gina to Pizza;
            move Blouse to Gina;
            "~Hey, Lane,~ you say. ~Shouldn't you be down at the Pizzeria?~^^
            Lane's eyes widen. ~Oh, shoot, sorry! Sorry!~^^
            She continues to apologize as she rushes out into the hall.";
        Kiss:
            "Lane pushes you away. ~Save it for later, all right?~";
        ],
        after [;
        AimAt:
            "^She rolls her eyes.";
        Shoot:
            "With the gun pointed right at Lane, you pull the trigger.
            Nothing happens.^^She sighs. ~Cut that out.~";
        ],
    has female animate proper;

Object Blouse "pink blouse" Lane
    with
        name 'pink' 'blouse',
        description [;
            if(location has backstage) "The blouse looks just fine on Lane.";
            "Gina's favorite blouse brings out the rosiness of her 
            complexion, even as dulled as it has become by the tragedies
            that life has delivered to her.";
        ],
    has clothing worn;

! Section - The Satchel

Object Satchel "satchel" 
    with
        name 'satchel' 'pouch' 'sack' 'bag',
        description [;
            if(location has backstage) 
                "This is not real suede.";
            if(Sunglasses has worn)
                "It's a small suede pouch, as luxurious as the
                prize it contains.";
        ],
        before [;
        Open:
            <<Search self>>;
        Search:
            if(location has backstage) "Yeah, it's empty.";
            if(Sunglasses has worn) 
                "Yes, they're all in here. The jewels that caused you so
                much trouble, trouble that caused you to commit so many
                crimes, crimes that caused you to spend so long in prison.";
            "What should be in here? It could be anything. But it ought
            to be something good.";
        Show: 
            if(second == Pauline) {
                if(Sunglasses has worn) {
                    deadflag = GS_DEATHMESSAGE;
                    finalmessage = "Right after we hire a writer.";
                    print "~The jewels!~ Pauline gasps, her breathlessness as
                        much a factor of her surprise as a consequence of her
                        illness. ~Then you were able to convince...~^^";
                    if(suspect == Gina) print "~Your mother";
                    else print "~Mister Craverly";
                    print ", yes. The jewels are mine", (u8212) 0,
                        "ours, now.~^^
                        ~Sidney, we can sell the jewels for the money for the
                        medical treatment that will save my life!~ Pauline's
                        eyes are huge and wet beneath the hospital lights as
                        they plaintively gaze upward into your own.^^
                        ~Sugarbabe, those were my thoughts exactly.~^";
                    pressanykey();
                    "~Why is Sidney nice all of a sudden?~^^
                    ~Well, I thought about just abandoning Janine, but I 
                    realized I had a chance to push things in a bold new
                    direction.~^^
                    ~It's Pauline. And that's not gonna work. People love
                    Sidney the evil twin; they're not going to respond well
                    to Sidney the altruist. Besides, Pauline has to stay
                    sick until Lisa has her baby.~^^
                    ~Oh. Do we need to shoot the whole thing again?~^^
                    ~There's no time. We'll figure something out.~";
                } else {
                    "~What's in that bag, Doctor Langridge?~ asks sweet,
                    innocent Janine.^^
                    You consider the question with all the weight it merits.^^
                    ~I have here some medicine. But it won't help you. It's a
                    special medicine, for a different patient. I shouldn't tell 
                    you too much about it. Never mind.~";
                }
            } else if(second == Gina) {
                if(Sunglasses has worn) {
                    if(suspect == Gina) 
                        "~Get that out of my face, Sid,~ Gina grouses.";
                    deadflag = GS_DEATHMESSAGE;
                    finalmessage = "We're definitely hiring a writer, though.";
                    print "Gina cocks her head at the appearance of the suede
                        pouch. ~Hey, scumbag! What's in the bag? Is it scum?~^^
                        ~It's a pile of jewels. They're worth a lot of money.
                        And I'm giving them to you.~ Gina's jaw drops, but you
                        continue. ~Then you're going to sell them, and then
                        you're going to use the money to open a new Gina's
                        Pizzeria location. And you're going to make me part
                        owner. And we're going to be rich.~^^
                        Gina can't think of anything to say.^";
                    pressanykey();
                    "~We definitely can't afford a second restaurant
                    set, though.~^^
                    ~We can work around that. Or, do you want us to
                    shoot it again?~^^
                    ~No, there's no time. We're already editing this one.
                    You can go home.~^^
                    ~So, does that mean...~^^
                    ~Oh, right. No, you're not fired.~";
                } else {
                    "~Gina, what do you think is in this bag?~ you ask.^^
                    ~I don't know, Doc. Is it doll hands?~^^
                    ~Yes, and,~ you say, ~if you see any other doll parts,
                    make sure you let me know.~";
                }
            } else if(second == Leopold) {
                if(Sunglasses has worn) {
                    if(suspect == Leopold) {
                        "'Don't rub it in, Sidney,' Leopold grouses.";
                    } else {
                        deadflag = GS_DEATHMESSAGE;
                        finalmessage = "...I'm gonna be busy looking for a writer.";
                        print "~What's that you've got there?~ asks Leopold,
                            clearly curious about the contents of the
                            mysterious pouch that you've produced.^^
                            ~These are the jewels,~ you explain, ~the jewels
                            that I spent so long in prison for. I stole them
                            on your orders! But you betrayed me. But now
                            they're mine!~^^
                            ~And what are you going to do with them?~ asks
                            Leopold, the shadows of confusion darting across
                            his weathered brow.^^
                            ~I'm going to buy Craverly Manor, and you're going
                            to be my butler.~";
                        pressanykey();
                        "~That doesn't make a lot of sense.~^^
                        ~Well, maybe Leopold could turn out
                        to be in debt?~^^
                        ~Sure, I guess. But I would have liked it better
                        if we spread out the whole saga of Sidney
                        returning, Sidney getting the jewels, and Sidney
                        demanding to buy the manor over the course of
                        twelve or twenty episodes.~^^
                        ~Oh. Do you want us to shoot it again?~^^
                        ~There's no time. We'll just edit this one until it
                        works. You go home and get something to eat.~^^
                        ~Yeah, we're all going down to the Gas Leak. You
                        wanna join us?~^^~No thanks...~";
                    }
                }
                "~Where did you get that?~ Leopold cries, shocked and 
                aghast.^^You raise your eyebrows. ~You mean, where did I 
                get this... bag of marbles?~^^Leopold regains his 
                composure. ~Ah, yes. Never mind.~";
            }
        ],
    has openable open container;

Object Protagonist "you"
    with
        name 'me' 'myself' 'self',
        short_name  "yourself",
        description [;
            if(location has backstage) {
                "You have no idea what you're doing.";
            } else if(Sunglasses has worn) {
                "You are Sidney Langridge, feared and hated in the town of
                Craverly Heights and beyond. Only in appearance are you
                identical to Doctor Langridge, for in words and deeds you are
                the polar opposite of your do-gooder twin.";
            } else {
                "You are Doctor Langridge, mentor to some, friend to many,
                and healer to all of the citizens of Craverly Heights.
                Everyone in town knows your name. It is Doctor Langridge.";
            }
        ],
        react_before [;
            ! stop you from doing stuff with dead bodies
            if(attempto()) rtrue;
        Listen:
            if(location has backstage) "It's pretty quiet.";
        ],
        each_turn [;
            updatetarget();
        ],
        before [;
        Shoot:
            if(location has backstage)
                "On days like this, it's not the last thought that
                occurs to you.";
            if(Sunglasses has worn) {
                "With the pistol aimed at your temple, you almost pull the
                deadly trigger that could free the world from the scourge of
                the existence of the dastardly Sidney Langridge, but you pull
                the gun away from your head, suddenly.^^~Sorry to disappoint,~
                you announce, ~But Sid Langridge is gonna be around for a
                while longer. I've got things to do.~^^
                You sneer menacingly.";
            }
            deadflag = GS_DEATHMESSAGE;
            finalmessage = "We're bringing back your evil twin.";
            print "~I've had it,~ you announce. ~I can't go on. All the
                secrets. All the lies. I'm finished. This is the end for your
                beloved Doctor Langridge. And little do you know: the secret
                of Wendell's death dies with me!~^^
                BLAM^";
            pressanykey();
            "~Why. Why did you do that.~^^
            ~It's like I said! I can't go on. I quit. Craverly Heights is
            a sinking ship, and this rat is jumping off.~^^
            ~First of all, that remark is offensive to rats. Second, you
            can't quit. Your contract has you in this studio for the next
            three years.~^^
            ~Well, what are you going to do? Doctor Langridge is dead.~^^
            ~Oh, that's not a problem...~";
        ],
        add_to_scope FauxThing,
        capacity MAX_CARRIED,
    has concealed animate proper transparent;

[Initialise u1 u2;
    lookmode = 2; ! VERBOSE
    player = Protagonist;
    SetTime(9*60+30, 1); ! 9:30 am, each turn 1 minute
    ! unicode is not available in v3
    ! for v5 (and v8) we need to check if the interpreter provides unicode
    has_unicode = 0;
#iftrue (#version_number >= 5);
    if((0 -> 50) >= 1) { ! standard revision number (as reported by the terp)
        @check_unicode 8212 -> u1;
        @check_unicode 10020 -> u2;
        has_unicode = ((u1 & 1) && (u2 & 1)); ! check if able to write
    }
#ifnot;
    u1 = u2 = 0; ! avoid compiler warnings
#endif;
];
